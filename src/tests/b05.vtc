varnishtest "regex compilation failure"

server s1 {
	rxreq
	txresp -hdr "Foo: bar" -hdr "Baz: quux" -body "foobar"
} -start

varnish v1 -vcl+backend {
	import re from "${vmod_topbuild}/src/.libs/libvmod_re.so";

	sub vcl_init {
	        new paren = re.regex("(");
	}

	sub vcl_recv {
	    	if (!paren.failed()) {
		        return(synth(999, "paren should have failed"));
		}
	}

	sub vcl_backend_response {
		if (paren.match(beresp.http.foo)) {
			set beresp.http.foo = "baz";
		}
		else {
		     set beresp.http.error = paren.error();
		}
	}
} -start

client c1 {
	txreq
	rxresp 
	expect resp.status == 200
	expect resp.http.content-length == 6
	expect resp.http.foo == "bar"
	expect resp.http.baz == "quux"
	expect resp.http.error != <undef>
	expect resp.http.error != ""
} -run

logexpect l1 -v v1 -d 1 -g vxid -q "VCL_Error" {
	expect 0 * Begin req
	expect * = VCL_Error "vmod re: error compiling regex:"
	expect * = End
} -run

varnishtest "regexp match and no-match (cf. varnish b00028.vtc & v00016.vtc)"

server s1 {
	rxreq
	txresp -hdr "Foo: bar" -hdr "Bar: foo" -body "1111\n"
} -start

varnish v1 -vcl+backend {
	import re from "${vmod_topbuild}/src/.libs/libvmod_re.so";

        sub vcl_recv {
                if ((re.match(req.url, "foobar"))) {
                        return(pass);
                } else if (re.match(req.url, "snafu")) {
                        return(pipe);
                } else if ((re.match_dyn(req.url, "foobar"))) {
                        return(pass);
                } else if (re.match_dyn(req.url, "snafu")) {
                        return(pipe);
                } else {
                        return(pass);
                }
        }

	sub vcl_fetch {
		if (re.match(beresp.http.foo, "bar")) {
			set beresp.http.foo1 = "1";
		} else {
			error 999;
		}

		if (!re.match(beresp.http.bar, "bar")) {
			set beresp.http.bar1 = "2";
		} else {
			error 999;
		}

		if (re.match_dyn(beresp.http.foo, "bar")) {
			set beresp.http.foo1d = "1";
		} else {
			error 999;
		}

		if (!re.match_dyn(beresp.http.bar, "bar")) {
			set beresp.http.bar1d = "2";
		} else {
			error 999;
		}
	}

} -start

client c1 {
	txreq
	rxresp
	expect resp.http.foo1 == "1"
	expect resp.http.bar1 == "2"
	expect resp.http.foo1d == "1"
	expect resp.http.bar1d == "2"
} -run

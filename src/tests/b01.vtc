varnishtest "regexp match and no-match (cf. varnish b00028.vtc & v00016.vtc)"

server s1 {
	rxreq
	txresp -hdr "Foo: bar" -hdr "Bar: foo" -body "1111\n"
} -start

varnish v1 -vcl+backend {
	import re from "${vmod_topbuild}/src/.libs/libvmod_re.so";

	sub vcl_init {
	        new foobar = re.regex("foobar");
		new snafu = re.regex("snafu");
		new bar = re.regex("bar");
	}

        sub vcl_recv {
                if (foobar.match(req.url)) {
                        return(pass);
                } else if (snafu.match(req.url)) {
                        return(pipe);
                } else {
                        return(pass);
                }
        }

	sub vcl_backend_response {
		if (bar.match(beresp.http.foo)) {
			set beresp.http.foo1 = "1";
		} else {
			set beresp.status = 999;
		}

		if (!bar.match(beresp.http.bar)) {
			set beresp.http.bar1 = "2";
		} else {
			set beresp.status = 999;
		}
	}

} -start

client c1 {
	txreq
	rxresp
	expect resp.status == "200"
	expect resp.http.foo1 == "1"
	expect resp.http.bar1 == "2"
} -run
